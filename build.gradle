buildscript {
    repositories {
        maven { url "https://maven.aliyun.com/repository/central" }
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'gradle.plugin.com.github.spotbugs:spotbugs-gradle-plugin:1.7.1'
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:2.1.8.RELEASE'
        classpath 'io.spring.gradle:dependency-management-plugin:1.0.8.RELEASE'
    }
}

wrapper {
    gradleVersion = '5.6'
}

subprojects {
    apply plugin: "java"
    apply plugin: "idea"
    apply plugin: "org.springframework.boot"
    apply plugin: "io.spring.dependency-management"

    group 'org.nkcoder'
    version '1.0'

    targetCompatibility = 1.11
    sourceCompatibility = 1.11

    repositories {
        mavenCentral()
        maven { url "https://maven.aliyun.com/repository/central" }
    }

    configurations {
        developmentOnly
        runtimeClasspath {
            extendsFrom developmentOnly
        }
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    apply from: rootProject.file('gradle/checkstyle/checkstyle.gradle')
    apply from: rootProject.file('gradle/spotbugs/spotbugs.gradle')
    apply from: rootProject.file('gradle/jacoco/jacoco.gradle')

    compileJava {
        options.compilerArgs << '-parameters'
    }

    ext {
        guavaVersion = '28.0-jre'
        jupiterVersion = '5.5.2'
    }

    dependencies {
        compileOnly 'org.projectlombok:lombok'

        annotationProcessor 'org.projectlombok:lombok'

        implementation "com.google.guava:guava:$guavaVersion"

        developmentOnly 'org.springframework.boot:spring-boot-devtools'

        testImplementation "org.junit.jupiter:junit-jupiter:${jupiterVersion}"
        testImplementation 'org.mockito:mockito-junit-jupiter'
        testImplementation('org.springframework.boot:spring-boot-starter-test') {
            exclude group: 'junit', module: 'junit'
        }
    }

    // use system property when run from command: ./gradlew -Dspring.profiles.active=dev bootRun
    bootRun {
        systemProperties System.properties
    }
}